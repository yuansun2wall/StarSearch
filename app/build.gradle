apply plugin: 'com.android.application'

def getApiKey() {
    Properties props = new Properties()
    try {
        props.load(new FileInputStream(project.file('secrets.properties')))
    } catch (ignore) {
        return ""
    }
    return props['API_KEY']
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.starsearch"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            buildConfigField "String", "API_KEY", "\"" + getApiKey() + "\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "API_KEY", "\"" + getApiKey() + "\""
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '28.0.3'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'org.apache.commons:commons-text:1.3'
    implementation project(':lib')
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.1'
    testImplementation 'org.robolectric:shadows-httpclient:4.1'
    //noinspection GradleDependency
    testImplementation "org.powermock:powermock-module-junit4:1.6.6"
    //noinspection GradleDependency
    testImplementation "org.powermock:powermock-module-junit4-rule:1.6.6"
    //noinspection GradleDependency
    testImplementation "org.powermock:powermock-api-mockito:1.6.6"
    //noinspection GradleDependency
    testImplementation "org.powermock:powermock-classloading-xstream:1.6.6"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

gradle.projectsEvaluated {
    preBuild.finalizedBy(rootProject.tasks.copyFiles)
}

// vim: ts=4:sw=4:et:ft=groovy

